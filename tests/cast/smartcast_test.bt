// SPDX-FileCopyrightText: 2023-present Lukas Neubert <lukas.neubert@proton.me>
// SPDX-License-Identifier: MPL-2.0


struct Foo{
	i i32
}
struct Bar{}

type Sum := Foo | Bar

struct Main {
	s Sum
}

fun get_sum_foo() Sum {
	return Foo{}
}

fun test_smartcast_scope() {
	f := get_sum_foo()

	if f is Bar {
		assert false
	} else {
		assert typeof(f) == 'Sum'
	}

	if f is Foo {
		assert typeof(f) == 'Foo'
	} else {
		assert false
	}
}

fun test_infix_scope() {
	f := get_sum_foo()

	assert f is Foo
}

type NumBit8 := i8 | u8

fun test_sumtype_builtins() {
	n := (1 as i8) as NumBit8

	if not (n is i8) {
		assert false
	}
}

fun test_cast_to_variant() {
	s := get_sum_foo()
	f := s as Foo
	assert f.i == 0

	m := Main{
		s = get_sum_foo()
	}
	mf := m.s as Foo
	assert mf.i == 0
}

fun get_sum() !Sum {
	return Foo{}
}

fun test_result() ! {
	s := get_sum()!
	assert s is Foo
}

fun test_struct_field() {
	m := Main{
		s = Foo{} // smartcast
	}
	assert m.s is Foo

	m2 := Main{
		s = Foo{} as Sum // explicit cast
	}
	assert m2.s is Foo
}

fun get_if_sum() Sum {
	return if 1 < 3 {
		Foo{}
	} else {
		Bar{}
	}
}

fun test_if_expr() {
	s := get_if_sum()
	assert s is Foo
}
